buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply from: 'https://raw.github.com/brunodecarvalho/gradle-plugins/master/integration-test.gradle'

jar {
    baseName = 'studyosaka8-jenkins-docker-app'
    version = '1.0.1210'
    manifest {
        attributes 'Implementation-Title': baseName, 'Implementation-Version': version
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Doma用ファイルコピー設定。
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources
compileJava.options.compilerArgs = ['-Adoma.dao.subpackage=impl', '-Adoma.dao.suffix=Impl']

// Test skip 可能設定。
def testOn = !Boolean.getBoolean('skip.tests')
test.onlyIf { testOn }
integrationTest.onlyIf { testOn }

// テスト時に引数をプログラム中からも拾えるように。
tasks.withType(Test) {
    systemProperties System.properties
}

eclipse {
    jdt {
        file {
            withProperties { properties ->
                properties.setProperty('org.eclipse.jdt.core.compiler.processAnnotations', 'enabled')
            }
        }
    }
}

eclipseJdt {
    doFirst {
        def factorypath = file('.factorypath')
        def writer = new FileWriter(factorypath)
        def xml = new groovy.xml.MarkupBuilder(writer)
        xml.setDoubleQuotes(true)
        xml.'factorypath'() {
            def domaJar = configurations.compile.find {
                it.name.startsWith('doma-2')
            }
            'factorypathentry'(kind: 'EXTJAR', id: domaJar, enabled: true, runInBatchMode: false)
        }
    }
}

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.seasar.doma.boot:doma-spring-boot-starter:1.1.0'
    compile 'org.flywaydb:flyway-core'
    compile 'mysql:mysql-connector-java'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.jopendocument:jOpenDocument:1.3'

    runtime 'com.h2database:h2'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit:4.12'
    testCompile 'com.codeborne:selenide:4.1'
}
